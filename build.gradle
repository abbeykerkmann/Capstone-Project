plugins {
	id 'org.springframework.boot' version '2.4.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.liquibase.gradle' version '2.0.3'
	id 'java'
}

group = 'seg4910.group17'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

sourceSets {
	intTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	intTestImplementation.extendsFrom implementation
	intTestRuntimeOnly.extendsFrom runTimeOnly
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'com.auth0:java-jwt:3.16.0'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	runtimeOnly 'org.postgresql:postgresql'
	liquibaseRuntime 'org.liquibase:liquibase-core:3.8.1'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.1.1'
	liquibaseRuntime 'mysql:mysql-connector-java:5.1.34'
	liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.6'
	liquibaseRuntime sourceSets.main.output
	liquibaseRuntime 'ch.qos.logback:logback-classic:1.2.3'
	liquibaseRuntime 'ch.qos.logback:logback-core:1.2.3'
	liquibaseRuntime 'org.slf4j:slf4j-api:1.7.25'
	liquibaseRuntime 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
//	testImplementation 'io.rest-assured:rest-assured:4.3.3'

	intTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    intTestImplementation 'org.assertj:assertj-core:3.11.1'
	intTestRuntimeOnly 'org.postgresql:postgresql'
}

task intTest(type: Test) {
	description = 'Runs integration Tests'
	group = 'verification'

	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
	useJUnitPlatform()
}

test {
	useJUnitPlatform()
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/db.changelog-master.xml'
			url 'jdbc:postgresql://localhost:5432/capstonedb'
			username 'capstonedb-user'
			password 'capstonedb-password'
		}
	}
}
